apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def prop(String key) {
    hasProperty(key) ? property(key).toString() : null
}

def ossrhUsername = prop("ossrhUsername")
def ossrhPassword = prop("ossrhPassword")

if (ossrhUsername != null) publishing.repositories {
    maven {
        url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
        name = "MavenCentral"
        credentials {
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

publishing.publications {
    maven(MavenPublication) {
        artifactId 'loader'
        groupId 'org.aya-prover.box2d-editor'
        version project.version
        from components.java

        pom {
            name = artifactId
            description = 'This project is a higher-order fork of a box2d editor loader for libgdx.'
            url = 'https://github.com/ice1000/box2d-editor'
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id = 'ice1000'
                    name = 'Tesla Zhang'
                    email = 'ice1000kotlin@foxmail.com'
                }
                developer {
                    id = 'simoarpe'
                    name = 'Simone Arpe'
                    email = 'simon.arpe@gmail.com'
                }
                // If only they can write these parts themselves...
                developer {
                    id = 'MovingBlocks'
                    name = 'Rasmus Praestholm et al.'
                    email = 'cervator@gmail.com'
                }
            }
            scm {
                url = 'https://github.com/ice1000/box2d-editor-2023'
                connection = "scm:git:${url}"
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

sourceSets {
    main {
        java.srcDirs = ["src"]
    }
}
